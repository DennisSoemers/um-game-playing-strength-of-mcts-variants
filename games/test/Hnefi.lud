(define "Custodial" 
    (custodial 
        (from (last To)) 
        Orthogonal
        (between (max 1) if:("IsPieceAt" #1 Next (between)) (apply (remove (between))))
        (to if:"FriendOrCentreOrFortress") 
    )
)

(define "EmptyButNotCentreOrFortress"
    (and (not (is In (between) (union (sites Centre) (sites "Fortresses")))) (is Empty (between)))
)

(define "FriendOrCentreOrFortress"
    (or ("IsFriendAt" (to)) (is In (to) (union (sites Centre) (sites "Fortresses"))))
)

(define "JarlIsCaptured" (apply (trigger "Surrounded" P2)))

(define "JarlWasCaptured" (is Triggered "Surrounded" P2))

//------------------------------------------------------------------------------

(game "Hnefi" 
    (players 2) 
    (equipment { 
        (board (square 7)) 
        (regions "Fortresses" (sites Corners))
        
        (piece "Thrall" P1 
            (move Slide
                Orthogonal 
                (between if:"EmptyButNotCentreOrFortress")
                (then 
                    (and {
                        ("Custodial" "Thrall2")
                        (surround (from (last To)) Orthogonal
                            (between
                                if:("IsPieceAt" "Jarl" P2 (between))
                                "JarlIsCaptured"
                            )
                            (to if:"FriendOrCentreOrFortress")
                        )
                    })
                )
            )
        )
        (piece "Thrall" P2 
            (move Slide
                Orthogonal 
                (between if:"EmptyButNotCentreOrFortress") 
                (then ("Custodial" "Thrall1")) 
            ) 
        ) 
        (piece "Jarl" P2 
            (move Slide
                Orthogonal 
                (then ("Custodial" "Thrall1")) 
            ) 
        ) 
    }) 
    (rules 
        (start { 
            (place "Thrall1" {"A2" "B1" "G4" "D1" "F1" "G2" "A6" "B7" "A4" "D7" "F7" "G6"})
            (place "Thrall2" {"C3" "C5" "E5" "E3"})
            (place "Jarl2" (centrePoint))
        })
        
        (play (forEach Piece)) 
        
        (end {
            ("PieceTypeReachWin" "Jarl2" (sites "Fortresses") P2)
            (if "JarlWasCaptured" (result P1 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata
    
    (info
        (rules "Played on an 11x11 board. The defending side comprises twelve soldiers and a king, who start the game in a cross formation in the center of the board.
                Their objective is for the king to escape by reaching any of the four corner squares.
                The attackers comprise 24 soldiers positioned in four groups of 6 around the perimeter of the board.
                The King piece is the only piece that can occupy the central and the four corner squares.
        All pieces move orthogonally any number of spaces, and pieces are taken by custodial capture (surrounding an enemy  piece on either side with two of a player's pieces). The king piece must be captured by surrounding it on all four sides.")
    )
    
    (graphics {
        (player Colour P1 (colour Black))
        (player Colour P2 (colour White))
        (board Style Tafl)
        (show Symbol "knotSquare" "Fortresses") 
    })

    
)



