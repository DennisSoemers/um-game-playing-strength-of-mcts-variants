(game "Slimetrail"
    (players 2)
    (equipment {
        (board (rotate -30 (hex Diamond 11)))	
        (piece "Snail" Shared 
            (move Step (to if:(is Empty (to)))
                (then 
                    (add 
                        (piece (id "Leaf" Neutral)) 
                        (to (last From)) 
                    )
                )
            )
        )
        
        (piece "Leaf" Neutral)
    })
    (rules 
        (start (place "Snail" (centrePoint)))
        (play (forEach Piece Shared))
        (end {
            (if (no Moves Next) (result Mover Win))
            (if (= (last To) 120) (result P1 Win))
            (if (= (last To) 0) (result P2 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata
    (info {
        (description "The Slimetrail game was invented by Bill Taylor in 1992 and it's a two player strategy abstract game.")
        (rules "Each player moves the same snail. You must move the snail to any adjacent place which doesn't have a leaf. Every time you move the snail, the previous place is filled by a leaf. The player wins when the snail is moved over the position with its colour or if the opponent is blocked.")
        (source "<a href=\"https://bodogemu.com/en/games/slimetrail\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Bodogemu</a>")
        (version "1.3.12")
        (credit "Eric Piette")
    })
    
    (graphics {
        (board Style Board)
        (board Colour InnerEdges (colour 160 140 100))
        (board Colour OuterEdges (colour Hidden))
        (board Colour Phase0 (colour Green))
        (show Edges Diagonal Hidden)
        (piece Scale "Snail" 0.8)
        (piece Colour "Leaf" fillColour:(colour Brown))
        (piece Colour "Snail" fillColour:(colour Brown))
        (show Symbol "hex" 0 fillColour:(colour Black) scale:0.8) 
        (show Symbol "hex" 120 fillColour:(colour White) scale:0.8) 
    })
)
